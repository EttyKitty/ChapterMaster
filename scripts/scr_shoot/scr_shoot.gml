/// @self Id.Instance.obj_enunit|Id.Instance.obj_pnunit
/// @param {Real} weapon_index_position Weapon number
/// @param {Id.Instance.obj_enunit|Id.Instance.obj_pnunit} target_object Target object
/// @param {Real} target_type Target dudes
/// @param {String} damage_data "att" or "arp" or "highest"
/// @param {String} melee_or_ranged melee or ranged
function scr_shoot(weapon_index_position, target_object, target_type, damage_data, melee_or_ranged) {
    try {
        // This massive clusterfuck of a script uses the newly determined weapon and target data to attack and assign damage
        var hostile_type;
        var hostile_damage;
        var hostile_weapon;
        var hostile_range;
        var hostile_splash;
        var aggregate_damage = att[weapon_index_position];
        var armour_pierce = apa[weapon_index_position];
        if (obj_ncombat.wall_destroyed == 1) {
            exit;
        }

        if ((weapon_index_position >= 0) && instance_exists(target_object) && (owner == 2)) {
            var shots_fired = wep_num[weapon_index_position];
            if (shots_fired == 0 || ammo[weapon_index_position] == 0) {
                exit;
            }
            var doom = 0;
            if ((shots_fired != 1) && (melee_or_ranged != "melee")) {
                switch (obj_ncombat.enemy) {
                    case eFACTION.ECCLESIARCHY:
                        doom = 0.3;
                        break;
                    case eFACTION.ELDAR:
                        doom = 0.4;
                        break;
                    case eFACTION.ORK:
                        doom = 0.2;
                        break;
                    case eFACTION.TAU:
                        doom = 0.4;
                        break;
                    case eFACTION.TYRANIDS:
                        doom = 0.4;
                        break;
                }
            }
            if (obj_ncombat.enemy == eFACTION.HERETICS) {
                aggregate_damage = round(aggregate_damage * 1.15);
            }
            if ((obj_ncombat.enemy == eFACTION.CHAOS) && (obj_ncombat.threat == 7)) {
                doom = 1;
            }

            var damage_type = "";
            var stop = 0;

            if (ammo[weapon_index_position] > 0) {
                ammo[weapon_index_position] -= 1;
            }

            if (damage_data == "medi") {
                damage_type = "att";
                if (aggregate_damage < armour_pierce) {
                    damage_type = "arp";
                }
            } else {
                damage_type = damage_data;
            }
            if (wep[weapon_index_position] == "Web Spinner") {
                damage_type = "status";
            }

            var attack_count_mod = max(1, splash[weapon_index_position]);

            if ((damage_type == "status") && (stop == 0) && (shots_fired > 0)) {
                var damage_per_weapon = 0, hit_number = shots_fired;
                if (melee_or_ranged != "wall") {
                    shots_fired *= attack_count_mod;
                }
                if ((hit_number > 0) && (melee_or_ranged != "wall") && instance_exists(target_object)) {
                    if (wep_owner[weapon_index_position] == "assorted") {
                        target_object.hostile_shooters = 999;
                    } else if (wep_owner[weapon_index_position] != "assorted") {
                        target_object.hostile_shooters = 1;
                    }
                    hostile_damage = 0;
                    hostile_weapon = wep[weapon_index_position];
                    hostile_type = 1;
                    hostile_range = range[weapon_index_position];
                    hostile_splash = attack_count_mod;

                    scr_clean(target_object, hostile_type, hit_number, hostile_damage, hostile_weapon, hostile_range, hostile_splash, armour_pierce);
                }
            } else if ((damage_type == "att") && (aggregate_damage > 0) && (stop == 0) && (shots_fired > 0)) {
                var damage_per_weapon, hit_number;

                damage_per_weapon = aggregate_damage;

                if (melee_or_ranged == "melee") {
                    if (shots_fired > (target_object.men - target_object.dreads) * 2) {
                        doom = ((target_object.men - target_object.dreads) * 2) / shots_fired;
                    }
                }

                hit_number = shots_fired;

                if ((doom != 0) && (shots_fired > 1)) {
                    damage_per_weapon = floor((doom * damage_per_weapon));
                    hit_number = floor(hit_number * doom);
                }
                if (melee_or_ranged != "wall") {
                    shots_fired *= attack_count_mod;
                }

                if ((hit_number > 0) && (melee_or_ranged != "wall") && instance_exists(target_object)) {
                    if (wep_owner[weapon_index_position] == "assorted") {
                        target_object.hostile_shooters = 999;
                    }
                    if (wep_owner[weapon_index_position] != "assorted") {
                        target_object.hostile_shooters = 1;
                    }
                    hostile_damage = damage_per_weapon / hit_number;
                    hostile_weapon = wep[weapon_index_position];
                    hostile_type = 1;
                    hostile_range = range[weapon_index_position];
                    hostile_splash = attack_count_mod;

                    scr_clean(target_object, hostile_type, hit_number, hostile_damage, hostile_weapon, hostile_range, hostile_splash, armour_pierce);
                }
            } else if (((damage_type == "arp") || (damage_type == "dread")) && (armour_pierce > 0) && (stop == 0) && (shots_fired > 0)) {
                var damage_per_weapon, hit_number;
                damage_per_weapon = aggregate_damage;
                if (aggregate_damage == 0) {
                    damage_per_weapon = shots_fired;
                }
                if (melee_or_ranged != "wall") {
                    shots_fired *= attack_count_mod;
                }
                if (melee_or_ranged == "melee") {
                    if (shots_fired > ((target_object.veh + target_object.dreads) * 5)) {
                        doom = ((target_object.veh + target_object.dreads) * 5) / shots_fired;
                    }
                }
                hit_number = shots_fired;

                if ((doom != 0) && (shots_fired > 1)) {
                    damage_per_weapon = floor((doom * damage_per_weapon));
                    hit_number = floor(hit_number * doom);
                }

                if (damage_per_weapon == 0) {
                    damage_per_weapon = shots_fired * doom;
                }

                if (hit_number > 0 && instance_exists(target_object)) {
                    hostile_weapon = wep[weapon_index_position];
                    hostile_range = range[weapon_index_position];
                    hostile_splash = attack_count_mod;
                    hostile_damage = damage_per_weapon / hit_number;
                    if (melee_or_ranged == "wall") {
                        var dest = 0;

                        hostile_damage -= target_object.ac;
                        hostile_damage = max(0, hostile_damage);
                        hostile_damage = round(hostile_damage) * hit_number;
                        target_object.hp -= hostile_damage;
                        if (target_object.hp <= 0) {
                            dest = 1;
                        }
                        obj_nfort.hostile_weapons = hostile_weapon;
                        obj_nfort.hostile_shots = hit_number;
                        obj_nfort.hostile_damage = hostile_damage;

                        scr_flavor2(dest, "wall", hostile_range, hostile_weapon, hit_number, hostile_splash);
                    } else {
                        target_object.hostile_shooters = (wep_owner[weapon_index_position] == "assorted") ? 999 : 1;
                        hostile_type = 0;

                        scr_clean(target_object, hostile_type, hit_number, hostile_damage, hostile_weapon, hostile_range, hostile_splash, armour_pierce);
                    }
                }
            }
        }

        if (instance_exists(target_object) && (owner == eFACTION.PLAYER)) {
            var shots_fired = 0;
            var stop = 0;
            var damage_type = "";

            if (weapon_index_position >= 0) {
                shots_fired = wep_num[weapon_index_position];
            }

            if (shots_fired == 0) {
                exit;
            }

            while (target_type < array_length(target_object.dudes_hp)) {
                if (target_object.dudes_hp[target_type] == 0) {
                    target_type++;
                    stop = 1;
                } else {
                    stop = 0;
                    break;
                }
            }

            if (weapon_index_position >= 0) {
                if (ammo[weapon_index_position] == 0) {
                    stop = 1;
                }
                if (ammo[weapon_index_position] > 0) {
                    ammo[weapon_index_position] -= 1;
                }
            }
            if (wep[weapon_index_position] == "Missile Silo") {
                obj_ncombat.player_silos -= min(obj_ncombat.player_silos, 30);
            }

            if (damage_data != "highest") {
                damage_type = damage_data;
            }
            if ((damage_data == "highest") && (weapon_index_position >= 0)) {
                damage_type = "att";
                if ((aggregate_damage >= 100) && (armour_pierce > 0)) {
                    damage_type = "arp";
                }
            }
            if (damage_data == "highest") {
                if (weapon_index_position == -51 || weapon_index_position == -52 || weapon_index_position == -53) {
                    damage_type = "att";
                }
            }

            if ((weapon_index_position >= 0) || (weapon_index_position < -40)) {
                // Normal shooting
                var that_works = false;

                if (weapon_index_position >= 0) {
                    if ((aggregate_damage > 0) && (stop == 0)) {
                        that_works = true;
                    }
                }
                if ((weapon_index_position < -40) && (stop == 0)) {
                    that_works = true;
                }

                if (that_works == true) {
                    var damage_per_weapon = 0;
                    attack_count_mod = 0;

                    if (weapon_index_position >= 0) {
                        damage_per_weapon = aggregate_damage / wep_num[weapon_index_position];
                    } // Average damage
                    if (weapon_index_position < -40) {
                        attack_count_mod = 3;

                        if (weapon_index_position == -51) {
                            at = 160;
                            armour_pierce = 0;
                        }
                        if (weapon_index_position == -52) {
                            at = 200;
                            armour_pierce = -1;
                        }
                        if (weapon_index_position == -53) {
                            at = 250;
                            armour_pierce = 0;
                        }
                    }

                    attack_count_mod = max(1, splash[weapon_index_position]);

                    // Armour multiplier indexed by AP rating (1..4); any AP outside that range
                    // leaves armour untouched. Infantry and vehicles scale differently.
                    var _inf_ap = [1, 3, 2, 1.5, 0];
                    var _veh_ap = [1, 6, 4, 2, 0];
                    var _ap_valid = (armour_pierce >= 1) && (armour_pierce <= 4);

                    // Never open fire on a dead rank/formation. Stale men/veh/medi (only refreshed on
                    // the enemy's own alarm) and scr_target's rank-1 fallback can aim us at corpses;
                    // snap to a living rank instead, or clean up the empty formation and bail.
                    if (!instance_exists(target_object)) {
                        exit;
                    }
                    if (target_object.dudes_num[target_type] <= 0) {
                        var _alive_rank = find_next_alive_rank(target_object, -1);
                        if (_alive_rank == -1) {
                            destroy_empty_column(target_object);
                            exit;
                        }
                        target_type = _alive_rank;
                    }

                    // Damage spills across ranks and, once a formation is spent, into the
                    // formation behind it. Every target actually fired upon gets its own flavour
                    // line with its own casualty count. The loop always terminates: shots_left
                    // strictly shrinks on every iteration that continues.
                    var spill_block = target_object;
                    var spill_rank = target_type;
                    var shots_left = shots_fired;
                    var touched_blocks = []; // Spill-over formations only; target_object is cleaned below.

                    // The whole volley posts ONE battle-log line: the first target gets the rich
                    // weapon flavour (deferred), and every later target the overflow kills is
                    // gathered into a kill list appended to it (see emit_volley_flavour).
                    var _first_target = true;
                    var _primary_flavour = undefined;
                    var _spill_kills = []; // [{ name, count }] for targets killed after the first.

                    while (shots_left > 0) {
                        // This target's armour against our AP rating.
                        var _armour = spill_block.dudes_ac[spill_rank];
                        if (_ap_valid) {
                            var _ap_table = spill_block.dudes_vehicle[spill_rank] ? _veh_ap : _inf_ap;
                            _armour *= _ap_table[armour_pierce];
                        }
                        var final_hit = max(0, (damage_per_weapon - (_armour * attack_count_mod)) * spill_block.dudes_dr[spill_rank]);

                        var rank_num = spill_block.dudes_num[spill_rank];
                        var rank_hp = spill_block.dudes_hp[spill_rank];
                        var total_damage = shots_left * final_hit;
                        var raw_kills = floor(total_damage / rank_hp);
                        var casualties = min(raw_kills, rank_num, shots_left * attack_count_mod);

                        // Surplus damage only spills once this rank is actually wiped out.
                        var next_shots = 0;
                        if ((casualties >= rank_num) && (rank_num > 0) && (raw_kills > rank_num)) {
                            next_shots = max(0, shots_left - ceil((rank_num * rank_hp) / final_hit));
                        }

                        // Gather flavour. The first target carries the rich phrasing (deferred, with
                        // the full weapon count); later targets just contribute to the kill list.
                        // final_hit <= 0 means armour stopped the shots cold (AP too low).
                        if (_first_target) {
                            _primary_flavour = scr_flavor(weapon_index_position, spill_block, spill_rank, shots_fired, casualties, final_hit <= 0, true);
                            _first_target = false;
                        } else if (casualties > 0) {
                            array_push(_spill_kills, { name: spill_block.dudes[spill_rank], count: casualties });
                        }

                        if ((rank_num == 1) && (casualties == 0) && (total_damage > 0)) {
                            spill_block.dudes_hp[spill_rank] -= total_damage; // Chip a lone survivor
                            if (spill_block.dudes_hp[spill_rank] <= 0) {
                                // Chipped to death: remove it now and drop the force count. Otherwise
                                // dudes_num stays 1 at dudes_hp <= 0 - a "zombie" that find_next_alive_rank
                                // skips, so it's never finished off and keeps inflating enemy_forces.
                                spill_block.dudes_num[spill_rank] = 0;
                                obj_ncombat.enemy_forces -= 1;
                            }
                        }
                        if (casualties >= 1) {
                            spill_block.dudes_num[spill_rank] -= casualties;
                            obj_ncombat.enemy_forces -= casualties;
                        }

                        shots_left = next_shots;
                        if (shots_left <= 0) {
                            break;
                        }

                        // Next target: a living rank in this formation, else the formation behind.
                        var next_rank = find_next_alive_rank(spill_block, spill_block.dudes_vehicle[spill_rank]);
                        if (next_rank == -1) {
                            spill_block = get_next_enemy_formation(spill_block);
                            if (spill_block == noone) {
                                break;
                            }
                            array_push(touched_blocks, spill_block);
                            next_rank = find_next_alive_rank(spill_block, -1);
                            if (next_rank == -1) {
                                break;
                            }
                        }
                        spill_rank = next_rank;
                    }

                    // Post the single consolidated line for the whole volley.
                    emit_volley_flavour(_primary_flavour, _spill_kills);

                    // Clean up the spill-over formations (target_object is handled below).
                    for (var _tb = 0; _tb < array_length(touched_blocks); _tb++) {
                        if (instance_exists(touched_blocks[_tb])) {
                            compress_enemy_array(touched_blocks[_tb]);
                            destroy_empty_column(touched_blocks[_tb]);
                        }
                    }
                }
            }

            if (stop == 0) {
                compress_enemy_array(target_object);
                destroy_empty_column(target_object);
            }
        }
    } catch (_exception) {
        ERROR_HANDLER.handle_exception(_exception);
    }
}

/// @function find_next_alive_rank
/// @description Returns the index of the next living rank (dudes_num > 0 and dudes_hp > 0) in a
///              formation, preferring ranks that match the requested vehicle flag. Returns -1 if
///              none. The dudes_hp > 0 check keeps callers safe from dividing by a rank's HP.
/// @param {Id.Instance} _block The obj_enunit formation to search.
/// @param {Real} _prefer_vehicle 0/1 to prefer that category, or -1 for any living rank.
/// @returns {Real}
function find_next_alive_rank(_block, _prefer_vehicle) {
    if (!instance_exists(_block)) {
        return -1;
    }
    var _fallback = -1;
    for (var f = 1; f <= 30; f++) {
        if (_block.dudes_num[f] <= 0 || _block.dudes_hp[f] <= 0) {
            continue;
        }
        if (_prefer_vehicle == -1 || _block.dudes_vehicle[f] == _prefer_vehicle) {
            return f;
        }
        if (_fallback == -1) {
            _fallback = f;
        }
    }
    return _fallback;
}

/// @function get_next_enemy_formation
/// @description Returns the nearest enemy formation (obj_enunit) sitting behind the given one
///              that still contains at least one living rank, or noone if there isn't one.
/// @param {Id.Instance} _block The formation we are spilling out of.
/// @returns {Id.Instance}
function get_next_enemy_formation(_block) {
    if (!instance_exists(_block)) {
        return noone;
    }
    var _bx = _block.x;
    var _bid = _block.id;
    var _best = noone;
    var _best_x = 0;
    with (obj_enunit) {
        if (id == _bid) {
            continue;
        }
        if (x <= _bx) {
            continue;
        }
        if (find_next_alive_rank(id, -1) == -1) {
            continue;
        }
        if (_best == noone || x < _best_x) {
            _best = id;
            _best_x = x;
        }
    }
    return _best;
}
